set (LILLYDAP_SRC 
	derbuf.c
	dermsg.c
	mem.c
	msgop.c
	opswi.c
	queue.c
	rfc1823.c
	msgop.tab  # Generated by gperf
)

add_custom_command (
	OUTPUT msgop.tab
	COMMAND gperf -m 10 ${CMAKE_CURRENT_SOURCE_DIR}/msgop.gperf --output-file=msgop.tab
	COMMENT "Run gperf to produce opcode table"
	VERBATIM
	SOURCES msgop.gperf
)

include_directories (${CMAKE_CURRENT_BINARY_DIR})  # For msgop.tab which was output

# Build LillyDAP both shared and static.
add_library (lillydapShared SHARED ${LILLYDAP_SRC})
set_target_properties (
	lillydapShared
	PROPERTIES OUTPUT_NAME lillydap)

add_library (lillydapStatic STATIC ${LILLYDAP_SRC})
set_target_properties (
	lillydapStatic
	PROPERTIES OUTPUT_NAME lillydap
)

if (NOT ${BUILD_SINGLE_THREADED})
	add_dependencies (lillydapStatic openpa-makefile)
	add_dependencies (lillydapShared openpa-makefile)
endif()	

install (
	TARGETS lillydapShared
	LIBRARY DESTINATION lib
	PUBLIC_HEADER DESTINATION include/lillydap
)

install (
	TARGETS lillydapStatic
	ARCHIVE DESTINATION lib
	PUBLIC_HEADER DESTINATION include/lillydap
)
